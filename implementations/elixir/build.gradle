
plugins {
  id 'network.ockam.gradle.commands' version '1.0.0'
}

// collect list of all directories in ockam/
def apps = []

def ockamDir = java.nio.file.Paths.get(projectDir.toPath().toString(), 'ockam').toFile()
ockamDir.eachFile(groovy.io.FileType.DIRECTORIES) {
  apps << projectDir.toPath().relativize(it.toPath()).toFile()
}

commands {
  group = 'ockam'

  rootDir = '../..'

  list = [
    build: [
      'mix local.hex --force --if-missing',
      'mix local.rebar --force',
      'mix deps.get',
      'mix compile'
    ],
    test: [
      'mix local.hex --force --if-missing',
      'mix local.rebar --force',
      'mix deps.get',
      'mix test'
    ],
    clean: [
      'mix local.hex --force --if-missing',
      'mix local.rebar --force',
      'mix clean'
    ],
    lint: [
      'mix local.hex --force --if-missing',
      'mix local.rebar --force',
      'mix deps.get',
      'mix lint'
    ],
    dialyze: [
      'mix local.hex --force --if-missing',
      'mix local.rebar --force',
      'mix deps.get',
      'mix dialyzer'
    ],
    veryClean: [
      'rm -rf _build deps'
    ]
  ]

  directories = apps
}

tasks.whenTaskAdded { task ->
  def isCleanTask = task.name.contains('clean') || task.name.contains('veryClean')
  if(!isCleanTask) {
    task.dependsOn(gradle.includedBuild('rust').task(':build_ockam_ockam_ffi'))
  }
}
